#include "protheus.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace custom.produtos.fornecedores
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC5,SC6", name="Produto X Fornecedor", country="ALL", initialRelease="12.1.2210")
//-------------------------------------------------------------------
/*{Protheus.doc} ProdFornTReportsBusinessObject
Classe para criação do Objeto de Negócio de Prod  x Forn para o TReports
 
@author Vanessa Ruama
@since 02/03/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class ProdFornTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object
 
    protected data aFields as array
    protected data aStruct as array
 
endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Vanessa Ruama
@since 02/03/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class ProdFornTReportsBusinessObject
_Super:new()
self:appendArea("Pedidos de Venda")
self:setDisplayName("Pedidos de Venda")
self:setDescription("Relatório de Pedidos de Venda")
 
self:aFields := {"C5_TIPO","C5_CLIENTE","C5_LOJACLI","C5_TIPOCLI","C5_CONDPAG","C5_NUM","C6_NUM","C6_ITEM","C6_PRODUTO","C6_UM","C6_QTDVEN","C6_VALOR","C6_TES","C6_LOCAL"}
self:aStruct := getStruct(self:aFields)
 
return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Vanessa Ruama
@since 02/03/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class ProdFornTReportsBusinessObject
local cQuery as character
local cAlias as character
local nSkip as numeric
local nCount as numeric
local nX as numeric
local jItems as json
local aPDFields as array
local lUseParams as logical
  
nCount := 0
cQuery := " SC5.C5_TIPO,SC5.C5_CLIENTE,SC5.C5_LOJACLI,SC5.C5_TIPOCLI,SC5.C5_CONDPAG,SC5.C5_NUM,SC6.C6_NUM,SC6.C6_ITEM,SC6.C6_PRODUTO, SC6.C6_UM,SC6.C6_QTDVEN,SC6.C6_VALOR,SC6.C6_TES,SC6.C6_LOCAL  FROM SC5010 AS SC5 INNER JOIN  SC6010 AS SC6 ON SC5.C5_NUM = SC6.C6_NUM ORDER BY SC5.C5_NUM"
lUseParams := .T.

//Os filtros serão setados na interface do novo TReports
if oFilter:hasFilter()
    cQuery += " AND " + oFilter:getSQLExpression()
endif

jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros
c
if ValType(jParams["01"]) <> "A" .and. ValType(jParams["02"]) <> "A"
    lUseParams := .F.
endif

if lUseParams .and. !empty(jParams["01"][1]) .and. !empty(jParams["02"][1])
    cQuery += "AND C5_CLIENTE BETWEEN " + jParams['01'][1] + " AND " +  jParams['02'][1]
endif
  
cAlias := MPSysOpenQuery(cQuery)
  
if nPage == 1
    (cAlias)->(dbGoTop())
else
    //Encontra a quantidade de itens que irá pular de acordo com a página atual
    nSkip := ((nPage - 1) * self:getPageSize())     
  
    (cAlias)->(dbSkip(nSkip))
endif  
  
//Verifica se precisa fazer o tratamento para LGPD
aPDFields := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:aFields)
lObfuscated := len( aPDFields ) != Len(self:aFields)

while !(cAlias)->(Eof())
    jItems := JsonObject():new()
 
    for nX := 1 To Len(self:aStruct)
        if lObfuscated .and. aScan(aPDFields, self:aStruct[nX][5]) == 0
            jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->&(self:aStruct[nX][5]))
        else
            jItems[self:aStruct[nX][1]] := (cAlias)->&(self:aStruct[nX][5])
        endif
    next nX
 
    self:oData:appendData(jItems)
 
    (cAlias)->(DBSkip())
    nCount++
  
    //Sai do loop quando chegar no tamanho de itens da página
    if nCount == self:getPageSize()
      exit
    endif
enddo 
  
//Se não for o último registro indica que terá próxima página
self:setHasNext(!(cAlias)->(Eof())) 
  
(cAlias)->(DBCloseArea())
    
return self:oData
 
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Vanessa Ruama
@since 02/03/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class ProdFornTReportsBusinessObject
Local nX as numeric
 
for nX := 1 To Len(self:aStruct)
    self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
Next nX

self:addParameter("01"  , "Fornecedor de", "string", .F.)
self:addParameter("02"  , "Fornecedor até", "string", .F.)

//Consulta dos parâmetros
self:setCustomURL("01", "api/framework/v1/genericLookupService/smartview/SC5", 2)
self:setCustomURL("02", "api/framework/v1/genericLookupService/smartview/SC5", 2)
  
return self:oSchema
 
//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos
 
@param aFlds array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Vanessa Ruama
@since 02/03/2023
@version 1.0
*/
//------------------------------------------------------------------- 
function getStruct(aFlds)
Local aConvFld as array
Local aFldTmp    as array
Local cCampo     as character
Local cFldQry    as character
Local cTipR      as character
Local nPos       as numeric
Local nC         as numeric
 
aConvFld := {{"C", "string"}, {"D", "date"}, {"N", "number"}, {"L", "boolean"}, {"M", "memo"}}
aFldTmp    := {}
 
for nC := 1 to Len(aFlds)
    cFldQry := aFlds[nC]
    nPos    := AT(".", aFlds[nC]) + 1
     
    if nPos > 0
        cCampo := Substr(cFldQry, nPos)
    else
        cCampo := cFldQry
    endif
     
    cTipo := GetSx3Cache(cCampo, "X3_TIPO")
     
    if (nPos := aScan(aConvFld, {|c| c[01] = cTipo})) > 0
        cTipR := aConvFld[nPos, 02]
    else
        cTipR := "string"
    endif
 
    AAdd(aFldTmp, {cCampo, FWSX3Util():GetDescription(cCampo), cTipR, FwX3Titulo(Upper(cCampo)), cCampo})
next nC
 
return (aFldTmp)
